cmake_minimum_required(VERSION 3.4.3)
include("../../../../cmake/modules/StandaloneOverlay.cmake")

include(OverlayXcodeExternalProject)

set(TARGET_SDKS_FOR_LEGACY_OVERLAY ${SWIFT_SDKS})
if(SWIFT_EXPERIMENTAL_COREFOUNDATION_OVERLAY_SDKS_FROM_REPO)
  list_intersect("${SWIFT_EXPERIMENTAL_COREFOUNDATION_OVERLAY_SDKS_FROM_REPO}" "${SWIFT_SDKS}" TARGET_SDKS_FOR_XCODE_OVERLAY)
  list_subtract("${SWIFT_SDKS}" "${TARGET_SDKS_FOR_XCODE_OVERLAY}" TARGET_SDKS_FOR_LEGACY_OVERLAY)
endif()

#
# Build the overlay from the Xcode project
#
if(TARGET_SDKS_FOR_XCODE_OVERLAY)
  message(STATUS "SDKs targeted by the new logic: ${TARGET_SDKS_FOR_XCODE_OVERLAY}")
  #TODO this should be a parameter that CMake receives
  set(FOUNDATION_OVERLAY_SOURCE_DIR "${CMAKE_SOURCE_DIR}/../swift-corelibs-foundation/Darwin")

  add_overlay_xcode_project(CoreFoundation
    SOURCE_DIR ${FOUNDATION_OVERLAY_SOURCE_DIR}
    BUILD_TARGET CoreFoundation-swiftoverlay
    ADDITIONAL_XCODEBUILD_ARGUMENTS ${ADDITIONAL_OVERLAY_XCODEBUILD_ARGS}
    TARGET_SDKS ${TARGET_SDKS_FOR_XCODE_OVERLAY})
  add_overlay_targets(CoreFoundation TARGET_SDKS ${TARGET_SDKS_FOR_XCODE_OVERLAY})
endif()

#
# Build the overlay from legacy sources in Swift repo
#
if(NOT TARGET_SDKS_FOR_LEGACY_OVERLAY)
  message(STATUS "All the target sdk are generated by the new logic, skipping legacy logic")
  return()
endif()

add_swift_target_library(swiftCoreFoundation ${SWIFT_SDK_OVERLAY_LIBRARY_BUILD_TYPES} IS_SDK_OVERLAY
  CoreFoundation.swift

  "${SWIFT_SOURCE_DIR}/stdlib/linker-support/magic-symbols-for-install-name.c"

  SWIFT_COMPILE_FLAGS ${SWIFT_RUNTIME_SWIFT_COMPILE_FLAGS} ${SWIFT_STANDARD_LIBRARY_SWIFT_FLAGS}
  LINK_FLAGS "${SWIFT_RUNTIME_SWIFT_LINK_FLAGS}"
  TARGET_SDKS ${TARGET_SDKS_FOR_LEGACY_OVERLAY}
  SWIFT_MODULE_DEPENDS_OSX Darwin Dispatch ObjectiveC # auto-updated
  SWIFT_MODULE_DEPENDS_IOS Darwin Dispatch ObjectiveC # auto-updated
  SWIFT_MODULE_DEPENDS_TVOS Darwin Dispatch ObjectiveC # auto-updated
  SWIFT_MODULE_DEPENDS_WATCHOS Darwin Dispatch ObjectiveC # auto-updated
  FRAMEWORK_DEPENDS CoreFoundation
  INSTALL_IN_COMPONENT sdk-overlay
)
